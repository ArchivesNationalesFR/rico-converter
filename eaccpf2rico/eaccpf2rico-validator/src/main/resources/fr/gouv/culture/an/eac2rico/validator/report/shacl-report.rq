PREFIX shacl:   <http://www.w3.org/ns/shacl#>

SELECT ?message ?severity ?nodeUri ?resultPath ?value ?sourceShape ?sourceConstraintComponent 
WHERE {
	?s a shacl:ValidationResult;
		shacl:focusNode ?nodeUri;
		shacl:resultSeverity ?resultSeverity;
		shacl:sourceConstraintComponent ?sourceConstraintComponent;
		shacl:sourceShape ?sourceShape .
	# resultPath is not here on AndConstraintComponent
	OPTIONAL { ?s shacl:resultPath ?resultPath; }
	# result message is not here on AndConstraintComponent
	OPTIONAL { ?s shacl:resultMessage ?message; }
	# value is not present for min cardinality violations
	OPTIONAL { ?s shacl:value ?value; }
	BIND(STRAFTER(STR(?resultSeverity), "#") AS ?severity)
}